plugins {
    id 'io.micronaut.build.internal.openapi-kotlin-kapt-generator-test-suite'
    alias(mn.plugins.kotlin.jvm)
    alias(mn.plugins.kotlin.kapt)
}

description = """
This project tests that the generated server sources can be compiled and
that tests can be ran with Micronaut 4
"""

dependencies {

    kapt(mnValidation.micronaut.validation.processor)
    kapt(mnSerde.micronaut.serde.processor)
    kapt(mn.micronaut.inject.kotlin)
    kapt(projects.micronautOpenapi)

    compileOnly(projects.micronautOpenapiAnnotations)
    compileOnly(mn.jackson.annotations)

    implementation(mn.micronaut.http)
    implementation(mnSerde.micronaut.serde.api)
    implementation(mn.micronaut.inject.kotlin)
    implementation(mn.jakarta.annotation.api)
    implementation(mnValidation.micronaut.validation)
    implementation(mnReactor.micronaut.reactor)
    implementation(mnData.micronaut.data.runtime)
    implementation(mn.kotlin.stdlib.jdk8)
    implementation(mn.kotlin.reflect)
    // Required when using useAuth=true
    implementation(mnSecurity.micronaut.security.oauth2)

    runtimeOnly(mnLogging.logback.classic)
    runtimeOnly(mnSerde.micronaut.serde.jackson)

    kaptTest(mnValidation.micronaut.validation.processor)
    kaptTest(mnSerde.micronaut.serde.processor)
    kaptTest(mn.micronaut.inject.kotlin)

    testCompileOnly(mn.micronaut.inject.kotlin.test)

    testImplementation(mn.micronaut.http.client)
    testImplementation(mnTest.micronaut.test.junit5)
    testImplementation(mnTest.junit.jupiter.params)

    testRuntimeOnly(mn.micronaut.json.core)
    testRuntimeOnly(mnSerde.micronaut.serde.jackson)
    testRuntimeOnly(mnTest.junit.jupiter.engine)
    testRuntimeOnly(mnLogging.logback.classic)
    testRuntimeOnly(mn.snakeyaml)
}

tasks.named("generateOpenApi") {
    generatorKind = "server"
    openApiDefinition = layout.projectDirectory.file("../test-suite-kotlin-ksp-server-generator/spec.yaml")
    outputKinds = ["models", "apis", "modelDocs", "supportingFiles", "modelTests", "apiTests"]
    parameterMappings = [
            // Pageable parameter
            [name: "page", location: "QUERY", mappedType: "io.micronaut.data.model.Pageable"],
            [name: "size", location: "QUERY", mappedType: "io.micronaut.data.model.Pageable"],
            [name: "sortOrder", location: "QUERY", mappedType: "io.micronaut.data.model.Pageable"],
            // Ignored header
            [name: "ignored-header", location: "HEADER"],
            // Custom filtering header
            [name: "Filter", location: "HEADER", mappedType: "io.micronaut.openapi.test.filter.MyFilter"]
    ]
    responseBodyMappings = [
            // Response with Last-Modified header mapping
            [headerName: "Last-Modified", mappedBodyType: "io.micronaut.openapi.test.dated.DatedResponse"],
            // Response with Page body
            [headerName: "X-Page-Number", mappedBodyType: "io.micronaut.data.model.Page", isListWrapper: true],
            [headerName: "X-Page-Count", mappedBodyType: "io.micronaut.data.model.Page", isListWrapper: true],
            [headerName: "X-Total-Count", mappedBodyType: "io.micronaut.data.model.Page", isListWrapper: true],
            [headerName: "X-Page-Size", mappedBodyType: "io.micronaut.data.model.Page", isListWrapper: true],
            // Ignored header - Does not wrap the response in HttpResponse
            [headerName: "ignored-header"]
    ]
}

kotlin {
    jvmToolchain(17)
}
